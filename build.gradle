buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://github.com/stil4m/maven-repository/raw/master/releases/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath 'com.palantir:jacoco-coverage:0.4.0'
    }
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url "https://github.com/stil4m/maven-repository/raw/master/releases/" }
    mavenCentral()
}

group 'at.kontofahren'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'kotlin-jpa'
apply plugin: "kotlin-allopen"
apply plugin: "kotlin-noarg"
apply plugin: 'war'

war {
    archiveName = 'government.war'
}

configurations {
    ktlint
}

allOpen {
    annotation("javax.ejb.Stateless")
    annotation("javax.persistence.Entity")
    annotation("javax.ejb.Singleton")
    annotation("utils.Open")
}

noArg {
    annotation("javax.ejb.Stateless")
    annotation("javax.persistence.Entity")
    annotation("javax.ejb.Singleton")
    annotation("utils.Open")
}

sourceCompatibility = 1.8

sourceSets {
    integrationTest {
        java.srcDir file('src/integration/kotlin')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        resources.srcDir file('src/integration/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

dependencies {
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.26'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
    compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'
    compile group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
    compileOnly group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.1.0.Final'
    compileOnly group: 'org.hibernate', name: 'hibernate-validator', version: '5.2.4.Final'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile 'com.github.s61-austria:auslandersintegrationskurs:v1.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile 'com.auth0:java-jwt:3.3.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile 'nl.stil4m:mollie-api:2.7.0'
    compile group: 'com.rabbitmq', name: 'amqp-client', version: '5.2.0'
    providedCompile group: 'javax', name: 'javaee-api', version: '8.0'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    ktlint "com.github.shyiko:ktlint:0.18.0"
    integrationTestCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    integrationTestCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    integrationTestCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    integrationTestCompile group: 'com.rabbitmq', name: 'amqp-client', version: '5.2.0'
    integrationTestCompile group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.5.0'
    integrationTestCompile group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    integrationTestCompile 'io.rest-assured:rest-assured:3.0.7'
    integrationTestCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
